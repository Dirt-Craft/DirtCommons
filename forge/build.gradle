buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = "https://repo.spongepowered.org/maven"}
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7.+'
    }
}
plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.+'
}
apply plugin: 'java-library'
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: "org.spongepowered.mixin"

String mixinRefmap = project.ext.refmap.toString()
String mixinConfig = project.ext.mixinConfig.toString()
version = project.ext.fullVersion
archivesBaseName = "${project.ext.name}-Forge-${project.ext.fullVersion}.jar"
group = "${project.ext.group}"

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

configurations {
    refreshGradle
    //shadowRun
    //shadowImpl
    //shadowAPI
    implementation.extendsFrom(shadowImpl)
    api.extendsFrom(shadowAPI)
    //shadowAPI.extendsFrom(api)
    runtime.extendsFrom(shadowRun)
}

repositories {
    flatDir { dirs 'libs' }
}

dependencies {
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
    minecraft 'net.minecraftforge:forge:1.16.5-36.1.25'
    shadowAPI project(':common')
    shadowImpl 'net.luckperms:api:5.3'
    runtimeOnly fg.deobf('net.luckperms:LuckPerms-Forge:5.3.474')
}

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    mappings channel: 'official', version: '1.16.5'

    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"
            args("--mixin.config", mixinConfig)
            workingDirectory(project.file("run"))
            singleInstance(true)

            mods {
                "${modId}" {
                    source sourceSets.main
                }
            }
        }
        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"
            args("--mixin.config", mixinConfig, "--nogui")
            workingDirectory(project.file("run"))
            singleInstance(true)

            mods {
                "${modId}" {
                    source sourceSets.main
                }
            }
        }
        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"
            args '--mod', modId, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                "${modId}" {
                    source sourceSets.main
                }
            }
        }
    }
}

def resourceTargets = ['META-INF/mods.toml', 'pack.mcmeta', "${mixinConfig}".toString()]
def intoTargets = ["$rootDir/forge/out/production/resources/", "$rootDir/forge/out/production/${project.name}.main/", "$rootDir/forge/bin/main/"]
def replaceProperties = [   version:        project.ext.fullVersion,
                            authors:        project.ext.authors,
                            description :   modDescription,
                            name   :        modName,
                            id     :        modId,
                            group  :        group,
                            refmap :        mixinRefmap]
processResources { //Thanks SizableShrimp#0755
    inputs.properties replaceProperties
    replaceProperties.put 'project', project
    filesMatching(resourceTargets) {
        expand replaceProperties
    }
    intoTargets.each { target ->
        if (file(target).exists()) {
            copy {
                from(sourceSets.main.resources) {
                    include resourceTargets
                    expand replaceProperties
                }
                into target
            }
        }
    }
}

jar {
    archiveName = archivesBaseName
    manifest {
        attributes([
                "Specification-Title": project.ext.name,
                "Specification-Vendor": project.ext.vendor,
                "Specification-Version": "${project.ext.fullVersion}",
                "Implementation-Title": project.ext.name,
                "Implementation-Version": "${project.ext.fullVersion}",
                "Implementation-Vendor" : project.ext.vendor,
                "TweakClass":"org.spongepowered.asm.launch.MixinTweaker",
                "TweakOrder": 0,
                "MixinConfigs":project.ext.mixinConfig,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

shadowJar {
    configurations = [project.configurations.shadowImpl, project.configurations.shadowAPI, project.configurations.shadowRun]
    relocate 'org.spongepowered.configurate',   'net.dirtcraft.libs.sponge.configurate'
    relocate 'io.leangen',                      'net.dirtcraft.libs.leangen'
    relocate 'com.typesafe',                    'net.dirtcraft.libs.typesafe'
    archiveName = archivesBaseName
    dependencies {

    }
}

task wrapper(type: Wrapper){
    gradleVersion = '7.2'
}

mixin {
    add(sourceSets.main, mixinRefmap)
    config mixinConfig
}

reobf {
    shadowJar { }
}

tasks.build.dependsOn reobfShadowJar
jar.finalizedBy('reobfShadowJar')
